<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:xs="http://www.w3.org/2001/XMLSchema"
    xmlns:c="http://baxter-it.com/config/component" xmlns:conf="http://baxter-it.com/config"
    xmlns:cm="http://baxter-it.com/price-engine/conf/client-messaging" exclude-result-prefixes="xs" version="2.0">

    <xsl:import href="baxterxsl:repo-base.xsl" />
    <xsl:import href="baxterxsl:text-fmt.xsl" />
    <xsl:import href="baxterxsl:conf-reference.xsl" />

    <xsl:output method="text" encoding="UTF-8" omit-xml-declaration="yes" />

    <xsl:param name="configurationProductId" />
    <xsl:param name="configurationComponentId" />
    <xsl:param name="configurationVariant" />

    <xsl:template match="/">
        <xsl:variable name="root">
            <xsl:copy-of select="conf:configuration-source/conf:request" />
            <xsl:apply-templates select="conf:configuration-source/conf:request" mode="load-document-with-variants">
                <xsl:with-param name="prefix" select="'client-messaging'" />
            </xsl:apply-templates>
        </xsl:variable>
        <xsl:apply-templates
            select="$root/cm:configuration/cm:clientMessaging[@id=$root/conf:request/conf:parameter[@id='cmid']/text()]" />
    </xsl:template>

    <xsl:template match="cm:clientMessaging">
        <xsl:text>#################################</xsl:text>
        <xsl:call-template name="CRLF" />
        <xsl:text># JAVA Properties File          #</xsl:text>
        <xsl:call-template name="CRLF" />
        <xsl:text># Generated by BaxterConfig  :) #</xsl:text>
        <xsl:call-template name="CRLF" />
        <xsl:text>#################################</xsl:text>
        <xsl:call-template name="CRLF" />
        <xsl:call-template name="CRLF" />
        <xsl:apply-templates select="*" />
    </xsl:template>

    <xsl:template match="cm:property">
        <xsl:value-of select="@id" />
        <xsl:text>=</xsl:text>
        <xsl:apply-templates />
        <xsl:call-template name="CRLF" />
    </xsl:template>

    <xsl:template match="conf:reference">
        <xsl:value-of select="@id" />
        <xsl:text>=</xsl:text>
        <xsl:apply-templates select="." mode="url" />
        <xsl:call-template name="CRLF" />
    </xsl:template>

</xsl:stylesheet>
